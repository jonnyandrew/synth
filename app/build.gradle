plugins {
    id("org.jmailen.kotlinter") version "3.5.1"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

def abi = project.getProperties().getOrDefault('abi', 'x86')
def keystorePropertiesFile = project.rootProject.file('keystore.properties')
def appVersionCode = Integer.valueOf(project.getProperties().getOrDefault('versionCode', 1))
def appVersionName = project.getProperties().getOrDefault('versionName', '1.0')

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "com.flatmapdev.synth"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
            }
        }
    }
    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                Properties properties = new Properties()
                properties.load(keystorePropertiesFile.newDataInputStream())
                storeFile file(properties['storeFile'])
                keyAlias properties['keyAlias']
                storePassword properties['storePassword']
                keyPassword properties['keyPassword']
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    ndkVersion '21.0.6113669'
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.10.2"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
        freeCompilerArgs += [
                "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
                "-Xopt-in=kotlinx.coroutines.FlowPreview",
                "-Xinline-classes"
        ]

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
        }
    }
    
    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'
        xmlReport false
    }

    buildFeatures {
       viewBinding true
    }
}

task cppTest {
    doLast {
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine 'chmod', '+x', './cpp_test_runner.sh'
        }
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine './cpp_test_runner.sh'
        }
    }

    dependsOn ':app:generateJsonModelDebug'
    dependsOn ':app:externalNativeBuildDebug'
}

task cppLint {
    doLast {
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine 'chmod', '+x', './cpp_lint_runner.sh'
        }
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine './cpp_lint_runner.sh'
        }
    }

    dependsOn ':app:generateJsonModelDebug'
    dependsOn ':app:externalNativeBuildDebug'
}

task cppLintFix {
    doLast {
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine 'chmod', '+x', './cpp_lint_runner.sh'
        }
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine './cpp_lint_runner.sh', '-fix'
        }
    }

    dependsOn ':app:generateJsonModelDebug'
    dependsOn ':app:externalNativeBuildDebug'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'
    implementation 'com.google.dagger:dagger:2.40.5'
    kapt 'com.google.dagger:dagger-compiler:2.40.5'
    implementation 'androidx.core:core-ktx:1.7.0'

    // ui
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'com.google.android.material:material:1.5.0'
    implementation "androidx.lifecycle:lifecycle-runtime:2.4.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.0"

    // test
    kaptTest 'com.google.dagger:dagger-compiler:2.40.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    // Must build with androidx.fragment:fragment-testing so that we can run release flavor unit
    // tests on the CI. But this should be changed from implementation to testImplementation when
    // its possible to do so.
    // See https://developer.android.com/training/basics/fragments/testing#configure
    implementation('androidx.fragment:fragment-testing:1.2.2', {
        exclude group: 'androidx.test'
    })
    testImplementation 'org.robolectric:robolectric:4.6.1'
    testImplementation 'io.mockk:mockk:1.10.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.4'
    testImplementation "androidx.navigation:navigation-testing:$nav_version"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
