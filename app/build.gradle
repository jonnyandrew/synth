apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.flatmapdev.synth"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                version "3.10.2"
                if (project.hasProperty("abiFilters")) {
                    abiFilters project.getProperty("abiFilters")
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    flavorDimensions "cpp"

    productFlavors {
        cpptest {
            dimension "cpp"
            externalNativeBuild {
                cmake {
                    arguments "-DRUN_UNIT_TESTS=TRUE"
                }
            }
        }

        cppprod {
            dimension "cpp"
            externalNativeBuild {
                cmake {
                    arguments "-DRUN_UNIT_TESTS=FALSE"
                }
            }
        }

    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.dagger:dagger:2.26'
    kapt 'com.google.dagger:dagger-compiler:2.26'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:2.2.1"
    implementation "androidx.navigation:navigation-ui-ktx:2.2.1"

    kaptTest 'com.google.dagger:dagger-compiler:2.26'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:1.7.1'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.fragment:fragment-testing:1.2.1'
    // Must build with androidx.fragment:fragment-testing so that we can run release flavor unit
    // tests on the CI. But this should be changed from implementation to testImplementation when
    // its possible to do so.
    // See https://developer.android.com/training/basics/fragments/testing#configure
    implementation('androidx.fragment:fragment-testing:1.2.1', {
        exclude group: 'androidx.test'
    })
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'io.mockk:mockk:1.9'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
