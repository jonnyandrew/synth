plugins {
    id("org.jmailen.kotlinter") version "2.3.1"
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.flatmapdev.synth"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++14"
                version "3.10.2"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
        allWarningsAsErrors = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'
        xmlReport false
    }
}

task cppTest {
    String abi = project.getProperties().getOrDefault('abi', 'x86')
    doLast {
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine 'chmod', '+x', './cpp_test_runner.sh'
        }
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine './cpp_test_runner.sh'
        }
    }

    dependsOn ':app:externalNativeBuildDebug'
}

task cppLint {
    String abi = project.getProperties().getOrDefault('abi', 'x86')
    doLast {
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine 'chmod', '+x', './cpp_lint_runner.sh'
        }
        exec {
            workingDir "./.cxx/cmake/debug/$abi"
            commandLine './cpp_lint_runner.sh'
        }
    }

    dependsOn ':app:externalNativeBuildDebug'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.dagger:dagger:2.26'
    kapt 'com.google.dagger:dagger-compiler:2.26'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    kaptTest 'com.google.dagger:dagger-compiler:2.26'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.12.2'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'androidx.fragment:fragment-testing:1.2.2'
    // Must build with androidx.fragment:fragment-testing so that we can run release flavor unit
    // tests on the CI. But this should be changed from implementation to testImplementation when
    // its possible to do so.
    // See https://developer.android.com/training/basics/fragments/testing#configure
    implementation('androidx.fragment:fragment-testing:1.2.1', {
        exclude group: 'androidx.test'
    })
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'io.mockk:mockk:1.9'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
