// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.21'
    // Using alpha version for the TestNavigationController
    ext.nav_version = "2.4.0"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Workaround for Robolectric issue https://github.com/robolectric/robolectric/pull/5432#issuecomment-576458990
String autoServiceVersion = "1.0-rc6"
allprojects {
    configurations.all { configuration ->
        configuration.resolutionStrategy {
            // Force latest versions of auto-service so we can perform appropriate dependency
            // substitution below.
            force "com.google.auto.service:auto-service:$autoServiceVersion"
            force "com.google.auto.service:auto-service-annotations:$autoServiceVersion"

            // Robolectric ships with a hard dependency on auto-service
            // https://github.com/robolectric/robolectric/issues/5245
            // PR pending: https://github.com/robolectric/robolectric/pull/5432
            String lowercaseName = configuration.name.toLowerCase(Locale.US)
            if (!lowercaseName.contains("compileonly")
                    && !lowercaseName.contains("kapt")
                    && !lowercaseName.contains("annotationprocessor")) {
                eachDependency { details ->
                    if (details.requested.group == 'com.google.auto.service') {
                        // Change the name to auto-service-annotations
                        // We can't tell Gradle to just ignore this dependency, but this works for our needs
                        details.useTarget group: details.requested.group, name: 'auto-service-annotations', version: autoServiceVersion
                    }
                }
            }
        }
    }
}
